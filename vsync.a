	processor 6502
	include "vcs.h"
	include "macro.h"

	SEG.U RAM_VARS
	ORG $80

; Line Vars
BlueLineX	ds 1
BlueLineCtr ds 1
BlueLineIdx ds 1

PinkLineX	ds 1
PinkLineCtr ds 1
PinkLineIdx ds 1

; Frame Vars
BlueFrameX	ds 1
BlueFrameCtr	ds 1
BlueFrameIdx	ds 1
PinkFrameX	ds 1
PinkFrameCtr	ds 1
PinkFrameIdx	ds 1

PinkRem	ds 1
BlueRem	ds 1

BluePad ds 1
PinkPad ds 1

Time	ds 2


; CODE
	SEG
	org  $f000

SongDataPinkInit
	.byte 70
SongDataPinkVel
	.byte 1,   0,  -1,  2, -2,  2,  0,  -1,  0, 1, 0, -1, 0
SongDataPinkTimes
        .byte 70, 30, 140, 40, 12, 40, 80,  40, 80, 40, 80, 40, 80,

SongDataBlueInit
	.byte 70
SongDataBlueVel
	.byte -1,  0,   1, -7,  0,  1, -1,  7, 0, -7, 0, 7, 0, -7, 0,
SongDataBlueTimes
        .byte 70, 30, 140, 21, 20, 50, 50, 21, 40, 21, 40, 21, 40, 21, 40,

    MAC SET_SPRT
    	sta WSYNC
        SLEEP 14
	lda {3}
	sec   
.loop
        sbc #15
        bcs .loop

	sta {1}
        
        sta WSYNC
        adc #7
        eor #$f
        
        asl
        asl
        asl
        asl
        sta {2}
        sta HMOVE
        lda #0
        sta {2}
           
    ENDM
    
    ; A -> A
    MAC CONVERT_TO_HM
    	asl
        asl
        asl
        asl
    ENDM
; Now we're going to drive the TV signal properly.
; Assuming NTSC standards, we need the following:
; - 3 scanlines of VSYNC
; - 37 blank lines
; - 192 visible scanlines
; - 30 blank lines

; We'll use the VSYNC register to generate the VSYNC signal,
; and the VBLANK register to force a blank screen above
; and below the visible frame (it'll look letterboxed on
; the emulator, but not on a real TV)


; at time t a bar can have a command to set its position, velocity
; and duration.



; Let's define a variable to hold the starting color
; at memory address $81

; The CLEAN_START macro zeroes RAM and registers
Start	CLEAN_START

	; Loading the song
	lda SongDataPinkInit
        sta PinkFrameX
        lda SongDataBlueInit
        sta BlueFrameX
        
        lda #0
        sta PinkFrameIdx
        sta BlueFrameIdx
        
        lda SongDataPinkTimes
        sta PinkFrameCtr
        lda SongDataBlueTimes
        sta BlueFrameCtr


NextFrame
; Enable VBLANK (disable output)
	lda #2
        sta VBLANK
; At the beginning of the frame we set the VSYNC bit...
	lda #2
	sta VSYNC
; And hold it on for 3 scanlines...
	sta WSYNC
	sta WSYNC
	sta WSYNC
; Now we turn VSYNC off.
	lda #0
	sta VSYNC

	; VBLANK - Game Logic Here
        
        lda #1
        clc
        adc Time
        sta Time
        lda #0
        adc Time+1
        sta Time+1



        ; Control the location of the paddles
        
        ldx PinkPad
        
        bit SWCHA
        bmi .NotRight
	inx
.NotRight
	bvs .NotLeft
        dex
.NotLeft
	
        cpx #140
        bcs .DontMove
	stx PinkPad
.DontMove

	lda #$aa
        sta COLUP0
        lda #$5c
        sta COLUP1
        
        lda #2
        sta ENAM0
        sta ENAM1
        
        lda #$30
        sta NUSIZ0
        sta NUSIZ1

	lda BlueFrameX
        sta BlueLineX
        SET_SPRT RESM0, HMM0, BlueLineX
        
        lda PinkFrameX
        sta PinkLineX
	SET_SPRT RESM1, HMM1, PinkLineX
        
        lda #$00
        sta GRP0
        sta GRP1
        
        lda PinkFrameIdx
        sta PinkLineIdx
        lda BlueFrameIdx
        sta BlueLineIdx
        lda PinkFrameCtr
        sta PinkLineCtr
        lda BlueFrameCtr
        sta BlueLineCtr
        
        ldy BlueLineIdx
        lda SongDataBlueVel,y
        CONVERT_TO_HM
        sta HMM0
        
        ldy PinkLineIdx
        lda SongDataPinkVel,y
        CONVERT_TO_HM
        sta HMM1
        
        
        ldx #32
LVBlank	sta WSYNC	; accessing WSYNC stops the CPU until next scanline
	dex		; decrement X
	bne LVBlank	; loop until X == 0
        
        ; SCANLINE 40
	lda #0
        sta VBLANK
        
        
; 192 scanlines are visible
	ldx #170
	lda #1
ScanLoop        

        
        ldy PinkLineIdx
	dec PinkLineCtr
        bne .1
        iny
        lda SongDataPinkTimes,y
        sta PinkLineCtr
        lda SongDataPinkVel,y
        CONVERT_TO_HM
        sta HMM1
.1:
        sty PinkLineIdx
        
        ldy BlueLineIdx
	dec BlueLineCtr
        bne .2
        iny
        lda SongDataBlueTimes,y
        sta BlueLineCtr
        lda SongDataBlueVel,y
        CONVERT_TO_HM
        sta HMM0
.2:
	sty BlueLineIdx
        
        sta WSYNC
	sta HMOVE
     
        dex
	bne ScanLoop
        
        
        stx ENAM0
        stx ENAM1
        
        ; SCANLINE 212 (But should be 210)
        
        ; TODO: Activate the paddles here
                
        lda #$ff
        ;sta GRP0
        sta GRP1
        
        ;SET_SPRT RESP0, HMP0, BluePad
        SET_SPRT RESP1, HMP1, PinkPad
        
	ldx #18
HudLoop        
	sta WSYNC
        
        ; TODO: Draw the paddles here
        
        
        dex
        bne HudLoop

	; SCANLINE 232
	lda #2
        sta VBLANK
; 30 lines of overscan to complete the frame

UpdateFramePink: subroutine
        ldy PinkFrameIdx
	dec PinkFrameCtr
        bne .3
        iny
        lda SongDataPinkTimes,y
        sta PinkFrameCtr
.3
        sty PinkFrameIdx
	lda PinkFrameX
        sec
        sbc SongDataPinkVel,y
        sta PinkFrameX
        
        
UpdateFrameBlue: subroutine
        ldy BlueFrameIdx
	dec BlueFrameCtr
        bne .3
        iny
        lda SongDataBlueTimes,y
        sta BlueFrameCtr
.3
        sty BlueFrameIdx
	lda BlueFrameX
        sec
        sbc SongDataBlueVel,y
        sta BlueFrameX
        
	ldx #30
LVOver	sta WSYNC
	dex
	bne LVOver
	
; Go back and do another frame
	jmp NextFrame
        
	org $fffc
	.word Start
	.word Start
        
